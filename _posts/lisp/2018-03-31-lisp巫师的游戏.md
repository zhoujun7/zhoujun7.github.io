---
layout: post
title:  "巫师的游戏"
date:   2018-03-31
---

# 巫师的花园

## 完整代码

```commonlisp
(defparameter *location* 'living-room)

(defparameter *allowed-commands* '(look walk pickup inventory))

(defparameter *nodes* 
  '((living-room (you are in the living-room. a wizard is snoring loudly on the couch.))
    (garden (you are in a beautiful garden. there is a well in front of you.))
    (attic (you are in the attic. there is a giant welding torch in the corner.))))

(defparameter *edges* 
  '((living-room (garden west door)
                 (attic upstairs ladder))
    (garden (living-room east door))
    (attic (living-room downstairs ladder))))

(defparameter *objects* '(whisky bucket frog chain))

(defparameter *object-locations* 
  '((whisky living-room)
    (bucket living-room)
    (chain garden)
    (frog garden)))

(defun test-hello ()
  (princ "hello wizard"))

(defun describe-location (location nodes)
  (cadr (assoc location nodes)))

(defun describe-path (edge)
  `(there is a ,(caddr edge) going ,(cadr edge) from here.))

(defun describe-paths (location edges)
  (apply #'append (mapcar #'describe-path (cdr (assoc location edges)))))

(defun objects-at (loc objs obj-locs)
  (labels ((at-loc-p (obj)
                     (eq (cadr (assoc obj obj-locs)) loc)))
          (remove-if-not #'at-loc-p objs)))

(defun describe-objects (loc objs obj-loc)
  (labels ((describe-obj (obj)
                         `(you see a ,obj on the floor.)))
          (apply #'append (mapcar #'describe-obj (objects-at loc objs obj-loc)))))

(defun look ()
  (append (describe-location *location* *nodes*)
          (describe-paths *location* *edges*)
          (describe-objects *location* *objects* *object-locations*)))

(defun walk (direction)
  (let ((next (find direction 
                    (cdr (assoc *location* *edges*)) 
                    :key #'cadr)))
       (if next
	(progn (setf *location* (car next))
	       (look))
	'(you cannot go that way.))))

(defun pickup (object)
  (cond 
    ((member object (objects-at *location* *objects* *object-locations*))
     (push (list object 'body) *object-locations*)
     `(you are now carrying the ,object))
    (t '(you cannot get that.))))

(defun inventory ()
  (cons 'items- (objects-at 'body *objects* *object-locations*)))

(defun game-read ()
  (let ((cmd (read-from-string (concatenate 'string "(" (read-line) ")"))))
       (flet ((quote-it (x)
                        (list 'quote x)))
             (cons (car cmd) (mapcar #'quote-it (cdr cmd))))))

(defun game-eval (sexp)
  (if (member (car sexp) *allowed-commands*)
      (eval sexp)
      '(i do not know that command.)))

(defun tweak-text (lst caps lit)
  (when lst
    (let ((item (car lst))
          (rest (cdr lst)))
         (cond 
           ((eq item #\space) 
            (cons item (tweak-text rest caps lit)))
           ((member item '(#\! #\? #\.)) 
            (cons item (tweak-text rest t lit)))
           ((eq item #\") 
            (tweak-text rest caps (not lit)))
           (lit 
            (cons item (tweak-text rest nil lit)))
           ((or caps lit) 
            (cons (char-upcase item) (tweak-text rest nil lit)))
           (t 
            (cons (char-downcase item) (tweak-text rest nil nil)))))))

(defun game-print (lst)
  (princ (coerce (tweak-text (coerce (string-trim "() " (prin1-to-string lst))
                                     'list)
                             t			   
                             nil)
                 'string))
  (fresh-line))

(defun game-repl ()
  (let ((cmd (game-read)))
       (unless (eq (car cmd) 'quit)
         (game-print (game-eval cmd))
         (game-repl))))

```



## 第一部分 游戏功能的制作

### 第一节 我的位置

```commonlisp
(defparameter *location* 'living-room)
```

#### 代码分析：

定义全局变量`*location*` 值为游戏人物所处当前位置。

### 第二节 节点

```commonlisp
(defparameter *nodes* 
  '((living-room (you are in the living-room. a wizard is snoring loudly on the couch.))
    (garden (you are in a beautiful garden. there is a well in front of you.))
    (attic (you are in the attic. there is a giant welding torch in the corner.))))

(defun describe-location (location nodes)
  (cadr (assoc location nodes)))
```

#### 代码分析：



第一步 创建地图

这里的节点可以理解为地图，因为游戏不能在同一张地图内走动，只能从一张地图移动到另一张地图。所以也可以理解为坐标。

这里给游戏创建了，三张地图。客厅(living-room)，花园(garden)，阁楼(attic)。并且绘制这三张地图，因为是文本游戏，所有便用文字描述这三张地图。

客厅内场景的描述是you are in the living-room. a wizard is snoring loudly on the couch.

花园内场景的描述是you are in a beautiful garden. there is a well in front of you.

阁楼内场景的描述是you are in the attic. there is a giant welding torch in the corner.

第二步 显示地图

因为是文本游戏，所谓的现实地图便是，打印出地图的文字描述。

`describe-location` 函数便是现实地图的函数，又或者说这函数打印出地图的文字描述。

#### 知识点：

##### `quote` `list` `'`

首先 `(quote (1 2 3 4 5))` 和 `'(1 2 3 4 5)` 完全等价。

接下来便只需要讨论`quote` 和 `list` 的区别了。

`quote` 只能接收一个参数，这个参数可以是任何形式，并且这个参数不对自身求值。

`list` 可以接受任意个参数，每一个参数都会对自身求值。 

##### `car` `cdr` `cadr` …… 等一类函数

这是一类函数，但他们的基础只有两个分别是`car` 和  `cdr`。

用`cons` 点对值来说，`car` 取得是第一个值。 `cdr`取得是后一个值。

而cadr的意思就是`(car (cdr …))` ，其他的一次类推。

可以有`caar` `cadr` `caddr` `caadr` `cddr` 等等。

##### `assoc` 

`assoc` 的形式大概如下：
`(assoc 'key '(第一项 第二项 第三项 ......))`

`assoc` 函数匹配到第一条之后便不在向下匹配。

首先巫师花园游戏的代码中`*nodes*` 是一个列表。

`(living-room (you are in the living-room. a wizard is snoring loudly on the couch.))`是列表的第一项，

`(garden (you are in a beautiful garden. there is a well in front of you.))` 是列表的第二项，

`(attic (you are in the attic. there is a giant welding torch in the corner.))`是列表的第三项。

每一项依次都要拿出来对比，直到每一项里面的第一项与`‘key` 相等则返回这一项。

若`'key` 是 `living-room` 则`(living-room (you are in the living-room. a wizard is snoring loudly on the couch.))`这一条符合规则返回这一条内容。以下项再有`living-room` 也不去管它了， `assoc` 函数运行退出。



### 第三节 边缘

```commonlisp
(defparameter *edges* '((living-room (garden west door) (attic upstairs ladder))
                        (garden (living-room east door))
                        (attic (living-room downstairs ladder))))

(defun describe-path (edge)
  `(there is a ,(caddr edge) going ,(cadr edge) from here.))

(defun describe-paths (location edges)
  (apply #'append (mapcar #'describe-path (cdr (assoc location edges)))))
```

#### 代码分析

所谓边缘就是，怎么走，角色的行进的方向。

拿`*edges*`举例子，我们一共定义了三张地图，分别是`living-room` `garden` `attic` 。

我们如果处于`living-room` 的位置，我们的边缘信息有两条分别是`(garden west door) (attic upstirs ladder)`。

在`living-room`的时候我们可以通往`garden`和`attic` ，他们分别是在`living-room`的`west` `upstirs` 方向，使用的道具分别是`door` `ladder`。

`describe-path`函数为我们描述一条边缘信息。而`describe-paths` 则是调用`describe-path` ,分别把每条边缘信息，传入之后，统一输出出来。

#### 知识点

##### \` , @

反引号 ` 效果与 单引号‘，基本相同。

不同的是反引号` 修饰的列表，里面的项可以用逗号,修饰。修饰之后，便可以对自身求值。

至于@ 符号，需要配合逗号一起使用，效果类型去掉，它后面跟着的括号。因为适合逗号配合使用的，所以它所修饰的项会对自身求值。

需要注意的是这里所谓不对自身求值，便不需要在意，表达式的第一项要是函数。而对自身求值，则需要注意表达式的第一项需要是函数。

```commonlisp
`(1 2 3 (+ 1 2 3)) => (1 2 3 (+ 1 2 3))
`(1 2 3 ,(+ 1 2 3)) => (1 2 3 6)
`(1 2 3 ,@(+ 1 2 3)) => (1 2 3 . 6) ;; 这要特别注意
`(1 2 3 ,(list 1 2 3)) => (1 2 3 (1 2 3))
`(1 2 3 ,@(list 1 2 3)) => (1 2 3 1 2 3)
`(1 2 3 ,'(1 2 3)) => (1 2 3 (1 2 3))
`(1 2 3 ,@'(1 2 3)) => (1 2 3 1 2 3)

;;下面是模拟@符号的使用
(cons 1 (cons (cons 2 3) nil)) => (1 (2 . 3))
(cons 1 (cons (cons 2 (cons 3 nil)) nil)) => (1 (2 3))
(cons 1 (cons       2 (cons 3 nil))) => (1 2 3)
(cons 1 (cons       2       3)) => (1 2 . 3)
```

##### lambda

lambda是匿名函数。定义函数的时候把 defun 函数名 整个替换成lambda,即可.

##### 高阶函数

参数是函数的函数，就是高阶函数。lisp有一些已经定义好的高阶函数。

函数作为参数可以使用匿名函数lambda，也可以使用已经定义好的函数，只需要在函数名前加上`#' 。

几个初期必须掌握的已经定义好的高阶函数。

`funcall` `apply`

这两个函数使用相差不大。第一个参数都是传入的函数。

功能就是调用在第一个参数传入的函数，并且把从第二个参数开始的参数依次传入第一个参数的函数中去。

这里特别注意的是apply函数的最后一个参数，有默认加了`,@ 这三个符号的感觉，也就是说apply函数最后的一个参数必须是列表，并且它会把列表的最外层货号脱去，再依次传入第二个参数的函数中去。

例子

```commonlisp
(defun foo (a b c d e f) 
  (list a b c d e f))
(funcall #'foo 1 2 3 4 5 6) => (1 2 3 4 5 6)
(apply #'foo 1 2 3 4 '(5 6)) => (1 2 3 4 5 6)
```

mapcar mapc

这两个函数的差别也不大，主要区别在返回值上面。

作为高阶函数它们传入的第一个参数是函数。

之后它们传入的参数必须是列表。第一个参数的函数有几个参数，后面就要传入几个列表。

然后根据列表的长度，决定，第一个参数函数的调用次数。mapcar把结果包装在列表里面。而mapc仅返回第二个参数的列表。

```commonlisp
(defun foo (x y)
	(+ x y))
(mapcar #'foo '(1 2 3) '(4 5 6)) => (5 7 9)
(mapc #'foo '(1 2 3) '(4 5 6)) => (1 2 3)
```



### 第四节 道具

```commonlisp
(defparameter *objects* '(whisky bucket frog chain))

(defparameter *object-locations* '((whisky living-room)
                                   (bucket living-room)
                                   (chain garden)
                                   (frog garden)))

(defun objects-at (loc objs obj-locs)
  (labels ((at-loc-p (obj)
                     (eq (cadr (assoc obj obj-locs)) loc)))
          (remove-if-not #'at-loc-p objs)))

(defun describe-objects (loc objs obj-loc)
  (labels ((describe-obj (obj)
                         `(you see a ,obj on the floor.)))
          (apply #'append (mapcar #'describe-obj (objects-at loc objs obj-loc)))))
```

#### 代码分析

两个变量，`*objects*` 是定义道具，或者称呼物品栏。`*object-locations*`是定义道具的位置或者成为道具状态。

`objects-at` 函数

功能主要是生成传入位置，所有道具的列表。方法主要分为两步。

第一步是定义一个函数内的临时函数 `at-loc-p`，检查传入物品的位置，是否和外部函数变量loc，相同。相同放回T，不同返回nil。

第二步是使用`remove-if-not`函数去掉，之前返回nil的物品，只保留返回T的物品，生成一个列表返回，

`describe-objects`函数

我把它称为打开道具栏。或者，显示周围物品。

同样它定义了一个函数内部的临时函数。`describe-obj`功能是现实一件物品的状态。

之后只用`mapcar` ,把每一件物品的状态都现实一下保存在列表内。最后通过`append`函数，把列表的每一项都连接起来。形成一整句好，我称为调整现实效果。

#### 知识点

`eq` `eql` `equal` `equalp` `=` `string-equal` `char-equal`

|                     | `eq` | `eql` | equal | equalp | =     | string-equal | char-equal |
| ------------------- | ---- | ----- | ----- | ------ | ----- | ------------ | ---------- |
| 'a 'a               | T    | T     | T     | T      | Error | T            | Error      |
| '(1 2 3) '(1 2 3)   | NIL  | NIL   | T     | T      | Error | Error        | Error      |
| #\a #\a             | T    | T     | T     | T      | Error | T            | T          |
| #\a #\A             | NIL  | NIL   | NIL   | T      | Error | T            | T          |
| "abc" "abc"         | NIL  | NIL   | T     | T      | Error | T            | Error      |
| "abc" "ABC"         | NIL  | NIL   | NIL   | T      | Error | T            | Error      |
| 1 1                 | T    | T     | T     | T      | T     | Error        | Error      |
| 1 1.0               | NIL  | NIL   | NIL   | T      | T     | Error        | Error      |
| #c(1 1) #c(1 1)     | NIL  | T     | T     | T      | T     | Error        | Error      |
| #c(1 1) #c(1.0 1.0) | NIL  | NIL   | NIL   | T      | T     | Error        | Error      |

`flet` `labels` 

这两个函数是用来定义临时的局部函数的。

区别是`flet`不能在定义的临时函数内，使用刚定义的临时函数。而labels可以。

例子

```commonlisp
(flet ((foo ()
            (princ "hello"))
       (bar ()
            (princ "world")))
      (foo)
      (bar))
=>helloworld

(labels ((foo ()
              (princ "hello"))
         (bar ()
              (foo)
              (princ "world")))
        (bar))
=> helloworld
```

`append`

这个函数就是把数个列表连接起来。所以，除了最后一个参数，以外，所有参数都必须是列表

`remove-if` `remove-if-not` 

函数按字面翻译，remove(移除) if(如果) ，所以第二个参数就是如果的条件啦，条件达成也就是返回真，则这一项移除。

下一个类似的函数,remove(移除) if(如果) not(不)，所以第二个参数就是如果不移除的条件啦，也可以理解成如果条件达不到则移除。

第三个参数也就是需要被看条件移除的列表了。但是列表本身是没有变化的，最终结果是按返回值返回的。

### 第五节 操作

```commonlisp
(defun look ()
  (append (describe-location *location* *nodes*)
          (describe-paths *location* *edges*)
          (describe-objects *location* *objects* *object-locations*)))

(defun walk (direction)
  (let ((next (find direction (cdr (assoc *location* *edges*)) :key #'cadr)))
       (if next
           (progn 
             (setf *location* (car next))
             (look))
           '(you cannot go that way.))))

(defun pickup (object)
  (cond 
    ((member object (objects-at *location* *objects* *object-locations*))
     (push (list object 'body) *object-locations*)
     `(you are now carrying the ,object))
    (t '(you cannot get that.))))

(defun inventory ()
  (cons 'items- (objects-at 'body *objects* *object-locations*)))
```

#### 代码分析

`look` 查看周围环境

这个函数很好理解，就是把所有描述，用append连接成一句话，然后打印出来。

`walk` 走动

```commonlisp
(assoc *location* *edges*) ;; 这里加上*location*的值是‘living-room
=> (living-room (garden west door) (attic upstairs ladder))
(cdr (assoc *location* *edges*)) => ((garden west door) (attic upstairs ladder))
(find direction (cdr (assoc *location* *edges*)) :key #'cadr) ;;这里加上direction的值是'west
=> (garden west door) ;; find函数的关键字参数:key 决定了，第一个参数与，第二个参数的每一项传入函数cadr的返回值进行对比，返回找到的相同的第一个项。
;; 最后把它赋值给 局部变量next。
```

下一步是用`if`函数判断如果next存在则执行代码

```commonlisp
(progn 
  (setf *location* (car next))
  (look))
```

如果next变量不存在，也就是find函数没有想到，与direction相同的项则返回一句话

```commonlisp
'(you cannot go that way.)
```

`pickup` 拣取物品

这个函数是一个有两个分支的语句。

第一个分支

```commonlisp
((member object (objects-at *location* *objects* *object-locations*)) 
 ;; 这是一条测试判断语句。member函数找不到会返回NIL，找到了则会返回找到的值，也可以认为是T。
 ;; object-at函数是查找周围有哪些物品，然后返回周围物品组成的一个列表。
 ;; 用member语句判断，你需要捡起的物品是否在周围，在周围才能拣取，不在周围当然是不能拣取的。
     (push (list object 'body) *object-locations*)
     `(you are now carrying the ,object))
;; 以上两条是第一个分支为T的情况下需要执行的语句。
;; push是吧要拣取的物品和 ‘body组成一个列表，压入，变量*object-locations*中去，表示物品已经拣取。
;; 最后再打印一句话表示东西我已经捡了。
```

第二个分支

```commonlisp
(t '(you cannot get that.)) 
;; 第二个分支直接给了T，也就是说如果第一个分支是NIL那第二个分支必然会执行
;; 第二个分支执行。必然第一个分支是nil。也就是说要拣取的东西不在周围。
;; 所以就打印一句话，我捡不到。
```



`inventory`

这个单词翻译过来就是库存的意思。

这个函数也很简单，因为`objects-at` 函数显示的是传入位置有哪些物品。

那把包裹传入进去，就是包裹里有哪些东西。在用cons 给列表前边加一项最为标题。这里用把cons改成push也没有问题的。

#### 知识点

局部变量的定义

`let` `let*` `symbol-macrolet`

`let`和`let*`的区别是`let*`在定义局部变量的时候，可以使用之前定义的变量，而`let`不可以。

`let*`和`symbol-macrolet`的区别则是，`let*`更像是值拷贝，而`symbol-macrolet`则更像引用传递。

特别注意的是`symbol-macrolet`定义内部变量的绝对不能和外部已经定义过的变量重名，但可以使用外部定义过的变量。

补充`define-symbol-macro`,是与`symbol-macrolet`相似的函数不过`define-symbol-macro`定义的是全局的变量。

不过，`define-symbol-macro`函数定义的变量与`symbol-macrolet`一样不能重名。不同的是还不能删除，我暂时找不到方法。故此尽量不用。

例子

```commonlisp
(let ((a 1) (b 2))
     (+ a b)) => 3

(let* ((a 1) (b a))
      (+ a b)) => 2

(symbol-macrolet ((a '(1 2)) (b (car a)))
                 (setf b 2)
                 a) => '(2 2)
```



`find` `find-if` `find-if-not`

语法：

```commonlisp
(find        item      sequence &key from-end start end key test test-not) => element
(find-if     predicate sequence &key from-end start end key              ) => element
(find-if-not predicate sequence &key from-end start end key              ) => element
```

`find-if`就是把`find`函数的`test`参数代替`item`参数。

`find-if-not`就是把`find`函数的test-not参数代替`item`参数。

函数流程

1. `sequence`参数的每一项传入`key`参数中。
2. `item`和`key`参数返回的值一同传入`test`或者`test-not`中。

关键字：

start 开始查找的地方，重0开始计算。

end 是结束查找的地方，但不包括。

from-end 的值为nil时，正常查找。值为T时为返项查找。

`progn`

语法：

```commonlisp
(progn form*) => result*
```

`form`是可变参数。多条`form`按顺序执行。只有最后一条有返回值。

`if`

语法：

```commonlisp
(if test-form then-form [else-form]) => result*
```

这是最常见的分支函数。

需要注意的是`then-form`和`else-form`这两个参数，只能有一条执行函数语句。

如果要执行多条函数语句可以内嵌`progn`函数。



`cond`

语法：

```commonlisp
cond {clause}* => result*
clause::= (test-form form*) 
```

`cond`函数只有第一条检测到的`test-form`为T的语句会被执行。之后下面的`test-form`再为T也不会被执行。



`member`

语法：

```commonlisp
(member        item      list &key key test test-not) => tail
(member-if     predicate list &key key) => tail
(member-if-not predicate list &key key) => tail
```



`push` `pop`

语法：

```commlisp
(push item place) => new-place-value
(pop place) => element
```

`push`函数会改变`place`的值，并返回一个新的list。

`pop`也会改变`place`的值，返回的是弹出的元素。

`cons`

语法

```commonlisp
(cons object-1 object-2) => cons
```

cons也叫点对值。`list`其实本质就是多个点对值的嵌套。点对值是一个重点，解释起来也比较复杂，最好配合图片。

ANSI Common Lisp 这一本教材上面讲的比较好。

`T` `NIL`

这是commonlisp的boolean类型，它的boolean型有些特殊，尤其注意0是真值。

下面大致归纳一下比较特殊要注意的的：

真值

`T` `0` `"nil"` `"NIL"` `"Nil"` `'false` `'f` `"false"`  `"False"` `"FALSE"`  `"f"` `"F"`

假值

`'()` `()` `'nil` `nil` 

## 第二部分 界面的制作

### 第一节 获取游戏操作命令

```commonlisp
(defun game-read ()
  (let ((cmd (read-from-string (concatenate 'string "(" (read-line) ")"))))
       (flet ((quote-it (x)
                        (list 'quote x)))
             (cons (car cmd) (mapcar #'quote-it (cdr cmd))))))
```

#### 知识点

`read-line`

语法：

```commonlisp
(read-line &optional input-stream eof-error-p eof-value recursive-p) 
=> line, missing-newline-p
```

功能就是获取输入。

`concatenate`

语法：

```commonlisp
(concatenate result-type &rest sequences) => result-sequence
```

功能是连接字符串。

`result-type`是序列类型符号。指定返回的序列类型。常用的是`'list` `'string`

`read-from-string`

语法：

```commonlisp
(read-from-string string &optional eof-error-p eof-value &key start end preserve-whitespace)
=> object, position
```

把字符串转换成symbol。

#### 代码分析

第一步 获取命令。`read-line` 获取输入。之后用concatenate连接字符串，在输入的字符串两边加上括号。最后把它转换成list. 赋值给cmd局部变量。

第二步 因为之前命令的参数都是symbol。所以要定义一个临时函数，把参数都变成symbol。

第三步 命令的第一项因为是函数，所以不变，之后的每一项，都要输入第二步的临时函数内转换成symbol.



### 第二节 解释游戏操作命令

```commonlisp
(defparameter *allowed-commands* '(look walk pickup inventory))

(defun game-eval (sexp)()
  (if (member (car sexp) *allowed-commands*)
      (eval sexp)
      '(i do not know that command.)))
```

#### 知识点

`eval`

语法：

```common lisp
(eval form) => result*
```

form是一个list，求值form。

#### 代码分析

这一个有两个分支的语句，成功则求值sexp。失败打印语句。

这个函数的主要重点在于，分支语句的判断上面。

使用`member`函数，判断sexp列表的一个的项，用`eval`运行sexp时的函数，是不是在变量`*allowed-commands*`里面。

### 第三节 优化游戏显示

```commonlisp
(defun tweak-text (lst caps lit)
  (when lst   
    (let ((item (car lst))
          (rest (cdr lst)))
         (cond 
           ((eq item #\space) 
            (cons item (tweak-text rest caps lit)))
           ((member item '(#\! #\? #\.))
            (cons item (tweak-text rest t lit)))
           ((eq item #\")
            (tweak-text rest caps (not lit)))
           (lit 
            (cons item (tweak-text rest nil lit)))
           ((or caps lit)
            (cons (char-upcase item) (tweak-text rest nil lit)))
           (t 
            (cons (char-downcase item) (tweak-text rest nil nil)))))))

(defun game-print (lst)
  (princ (coerce (tweak-text (coerce (string-trim "() " (prin1-to-string lst))
                                     'list)
                             t
                             nil)
                 'string))
  (fresh-line))
```

#### 知识点：

`when` `unless

语法：

```commonlisp
(when test-form form*) => result*
(unless test-form form*) => result*
```

`coerce`

语法

```commonlisp
coerce object result-type => result
```

`coerce`简单概括就是类型转换。object 转换成 result-type类型，输出结果result.

主要的常用result-type `'list` `'vector` `'string` `'character`



fresh-line`

语法：

```commonlisp
(terpri &optional output-stream) => nil
(fresh-line &optional output-stream) => generalized-boolean
```

区别

`terpri`运行一次换一行。 

`terpri` 效果等同 `(write-char #\Newline output-stream)`

`fresh-line` 如果已经是新行了便不换行。如果换行了则返回true，没有换行则是false.



`write-to-string` `prin1-to-string` `princ-to-string`

语法：

```commonlisp
(write-to-string object &key array base case circle escape gensym length level lines miser-width pprint-dispatch pretty radix readably right-margin)
=> string
(prin1-to-string object) => string
(princ-to-string object) => string
```

三个函数主要功能都是list转string。区别是：

`prin1-to-string` 类似于 `(write-to-string object :escape t)` , 会在适当的地方加入转义字符。

`princ-to-string` 类似于 `(write-to-string :escape nil :readably nil)` 不会在适当的地方加入转义字符。

`write-to-string`参数介绍

`pretty` 参数赋值boolean型，输出的参数是否需要修饰，

#### 代码分析：

这段代码，新的知识点不多。但确实整个游戏的难点，也是重点。

`game-print`函数

主要流程是把list转换为string，再在string两头加上括号，之后转换为list。

传入`tweak-text`中进行处理。完成之后，再转换为string，打印出来，最后换行。

`tweak-text`函数

- lst 参数是传入的要处理的列表。cap表示列表的第一个字母是否转换为大写。lit表示开始处理双引号中的内容。
  - `tweak-text`函数是一个递归函数，出口是`when`语句，条件是lst参数为空。
- 函数有两个局部变量，
  - 第一个局部变量item是把lst的第一项赋值给它(car lst)。
  - 第二个局部变量是rest是把列表的除第一个变量之外的项都赋值给它(cdr lst)。
- 使用`cond`函数进入判断处理阶段。该函数我们使用了6个分支，我大致分为4个大类。
  - 第一个是特殊字符的处理。
    - 空格处理
    - 句末标点符号处理
    - 双引号处理
  - 第二个是双引号内的文字忽略外面的双引号。
  - 第三个是首字母大写。
  - 第四个是常规执行。

### 第四节 进入游戏

```commonlisp
(defun game-repl ()
  (let ((cmd (game-read)))
       (unless (eq (car cmd) 'quit)
         (game-print (game-eval cmd))
         (game-repl))))
```

代码分析

第一步 `game-read`函数见上面，把读取并加工过的命令给局部变量cmd。

第二步 判断cmd是不是quit,是则退出，不是则循环。